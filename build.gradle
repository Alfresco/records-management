class AMPTask extends DefaultTask {

    def String dist = 'build/dist'
	def String amp = null
	def String module = null
	def String jar = null
	def String config = 'config'
	def String web = 'source/web'
	
	@TaskAction
	def assembleAMP() {
	
		// assemble the AMP file
		ant.zip(destfile: dist + '/' + amp, update: 'true') {			
		
			ant.zipfileset(file: module + '/module.properties')
			ant.zipfileset(file: module + '/file-mapping.properties')
			
			if (jar != null) {
				ant.zipfileset(file: jar, prefix: 'lib')
			}
			
			if (config != null) {
				ant.zipfileset(dir: config, prefix: 'config') {
					ant.exclude(name: '**/module.properties')
					ant.exclude(name: '**/file-mapping.properties')
				}
			}
			
			if (web != null) {			
				ant.zipfileset(file: web, prefix: 'web')
			}
		}	
	}
}

subprojects {

	apply plugin: 'java'	
	apply plugin: 'eclipse'
	
	sourceSets {
		main {
			java {
				srcDir sourceJavaDir
			}
		}
	}

	repositories { 
		flatDir {
			dirs libsDir
		}
	}

	dependencies {		     		
		compile fileTree(dir: 'war/WEB-INF/lib', include: '*.jar')     	
	}
	
	compileJava.doFirst {
		if (file('war/WEB-INF').exists() == false) {
			tasks.expandWar.execute()
		}
	}
	
	task cleanWar << {
		ant.delete {
			ant.fileset(dir: 'war', excludes: '*.war')
		}
	}
	
	task refreshWar (dependsOn: ['cleanWar', 'expandWar'])
	
	task expandWar << {	
		
		if (file(warFile).exists()) {
		
			println 'Expanding ' + warFileName + ' WAR'
			ant.unzip(src: warFile, dest: 'war') 
		} 
		else {
		
			println 'The ' + warFile + ' was not found.'
		}
	}	
	
	task amp(type: AMPTask)
	
	amp.doFirst {	
		// ensure the dist directory exists
		def distDir = file(dist)
		if (distDir.exists() == false) {
			distDir.mkdir()
		}
	}

	assemble.doLast {
		tasks.amp.execute()
	}
	
}